resources
| where type == "microsoft.web/sites"
| where tags['global-project'] in ({Applications})
| extend state = properties.state
| extend appName=tostring(tags['global-app'])
| extend componentCode=tostring(tags['component-code'])
| where componentCode != ""
| extend serverFarmId= tolower(tostring(properties.serverFarmId))
| project appName,id,name, state,componentCode,location,serverFarmId
| join kind=leftouter (
    resources
    | where type == "microsoft.web/serverfarms"
    | extend aspStatus = tostring(properties.status)
    | extend ase = tostring(properties.hostingEnvironment)
    | extend aspSkuTier = tostring(sku.tier)
    | extend aspSkuCapacity = tostring(sku.capacity)
    | extend serverFarmId=tolower(id)
    | project serverFarmId, asp=id, aspStatus,ase,aspSkuTier, aspSkuCapacity) on serverFarmId
| join kind=leftouter (
    resources
    | where type =~ 'microsoft.insights/components'
    | extend AssignResource=tostring(tags["parent-resource"])
    | project ain=id,AssignResource
    ) on $left.name==$right.AssignResource
| project id,appName, state,componentCode,location,  asp,aspStatus, aspSkuCapacity,aspSkuTier,ase,ain